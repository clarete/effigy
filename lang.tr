# -*- Mode: peg; -*-

Module     <- { "Module" { (Statement / Expression)+ } }
            / { "Module" (Statement / Expression) }
Code       <- { "Code" { (Statement / Expression)+ } }
            / { "Code" (Statement / Expression) }

Statement  <- { "Statement" Assignment }
Assignment <- { "Assignment" { Expression (Store / StoreLex) } }
Expression <- Attribute / BinOp / Call / Lambda / Function / Load / Unary / Value

BinOp      <- { "BinOp" Expression Atom Expression }
Unary      <- { "Unary" { Atom Expression } }
Attribute  <- { "Attribute" { Load (LoadAttr / MethodCall)+ } }
LoadAttr   <- { "LoadAttr" Atom }
LoadMethod <- { "LoadMethod" Atom }
MethodCall <- { "MethodCall" { LoadMethod CallParams } }

ScopeId    <- { "ScopeId" Atom }
Lambda     <- { "Lambda" { Params Code } }
            / { "Lambda" { ScopeId Params Code } }
Function   <- { "Function" { Atom Params Code } }
            / { "Function" { ScopeId Atom Params Code } }

# Callable Parameter rules
Params     <- { "Params" { Param+ }? }
Param      <- { "Param" Atom }

# Call Site rules
Call       <- { "Call" { Expression CallParams } }
CallParams <- { Expression+ }?

# Value rules
Value      <- { "Value" (Number / String / Boolean / List) }
Number     <- { "Number" Atom }
String     <- { "String" Atom }
Boolean    <- { "BOOL" Atom }
List       <- { "List" { Expression* }? }

Load       <- { "Load" Atom  }
Store      <- { "Store" Atom  }
StoreLex   <- { "StoreLex" Atom  }

Atom       <- !{ .* } .
