# -*- Mode: peg; -*-

Module     <- { "Module" ({ Statement+ } / Statement)? }
Code       <- { "Code" ({ Statement+ } / Statement)? }

# Statements & Expression Types
Statement  <- { "Statement" (Assignment / IfStm / WhileStm / TryStm / Function / ExprStm) }
Assignment <- { "Assignment" { Expression (Store / StoreLex) } }
ExprStm    <- Expression
WhileStm   <- { "WhileStm" { Expression Code } }
IfStm      <- { "IfStm" { Expression Code Code? } }

TryStm     <- { "TryStm" { Code CatchStm } }
CatchStm   <- { "CatchStm" { Load Store Code } }

Expression <- Attribute / Logical / BinOp / Call / Lambda / Load / Unary / Value

# Logical Operators
Logical    <- { "Logical" { Expression (LogicalTwo / LogicalMult) } }
LogicalTwo <- { LogicalOp LogicalRd }
LogicalMult<- { { LogicalOp LogicalRd }+ }
LogicalOp  <- Atom
LogicalRd  <- Expression

BinOp      <- { "BinOp" Expression Atom Expression }
Unary      <- { "Unary" { Atom Expression } }
Attribute  <- { "Attribute" { Load (LoadAttr / MethodCall)+ } }
LoadAttr   <- { "LoadAttr" Atom }
LoadMethod <- { "LoadMethod" Atom }
MethodCall <- { "MethodCall" { LoadMethod CallParams } }

ScopeId    <- { "ScopeId" Atom }
Lambda     <- { "Lambda" { Params Code } }
            / { "Lambda" { ScopeId Params Code } }
Function   <- { "Function" { Atom Params Code } }
            / { "Function" { ScopeId Atom Params Code } }

# Callable Parameter rules
Params     <- { "Params" { Param+ }? }
Param      <- { "Param" Atom }

# Call Site rules
Call       <- { "Call" { Expression CallParams } }
CallParams <- { Expression+ }?

# Value rules
Value      <- { "Value" (Number / String / Boolean / List) }
Number     <- { "Number" Atom }
String     <- { "String" Atom }
Boolean    <- { "BOOL" Atom }
List       <- { "List" { Expression* }? }

Load       <- { "Load" Atom  }
Store      <- { "Store" Atom  }
StoreLex   <- { "StoreLex" Atom  }

Atom       <- !{ .* } .
